
plugins {
    id "org.sonarqube" version "2.8"
    id "io.franzbecker.gradle-lombok" version "3.2.0"
    id "java"
    id "maven-publish"
    id "signing"
    id "com.github.ben-manes.versions" version "0.27.0"
    id "com.github.hierynomus.license" version "0.15.0"
}

apply plugin: "jacoco"

group = "de.alpharogroup"
version = "$projectVersion"
description = "file-worker"
sourceCompatibility = "1.8"

sonarqube {
    properties {
        property "sonar.projectKey", "lightblueseas_file-worker"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compileOnly("org.projectlombok:lombok:$lombokVersion")
    annotationProcessor("org.projectlombok:lombok:$lombokVersion")
    compile("commons-io:commons-io:$commonsIoVersion")
    compile("org.apache.commons:commons-lang3:$commonsLang3Version")
    compile("net.lingala.zip4j:zip4j:$zip4jVersion")
    compile("de.alpharogroup:crypt-api:$cryptApiVersion")
    compile("de.alpharogroup:throw-able:$throwAbleVersion")
    compile("de.alpharogroup:mystic-crypt:$mysticCryptVersion")
    compile("de.alpharogroup:silly-strings:$sillyStringsVersion")
    compile("de.alpharogroup:silly-io:$sillyIoVersion")
    compile("de.alpharogroup:silly-collections:$sillyCollectionsVersion")
    compile("de.alpharogroup:jobj-compare:$jobjCompareVersion")
    testCompile("de.alpharogroup:jobj-contract-verifier:$jobjContractVerifierVersion")
    testCompile("de.alpharogroup:test-objects:$testObjectsVersion")
    testCompile("org.meanbean:meanbean:$meanbeanVersion")
    testCompile("de.alpharogroup:meanbean-factories:$meanbeanFactoriesVersion")
    testCompile("org.testng:testng:$testngVersion")
    testCompile("junit:junit:$junitVersion")
    testCompile("org.mockito:mockito-core:$mockitoCoreVersion")
}

license {
    ext.year="2015"
    ext.owner="Asterios Raptis"
    header rootProject.file('src/main/resources/LICENSE.txt')
    excludes(["**/README",
              "**/README.md",
              "**/LICENSE",
              "**/NOTICE",
              "**/*.xml",
              "**/*.xsl",
              "**/*.xsd",
              "**/*.dtd",
              "**/*.html",
              "**/*.jsp",
              "**/*.jpa",
              "**/*.sql",
              "**/*.properties",
              "**/*.bat",
              "**/*.gradle",
              "**/*.MF",
              "**/*.txt",
              "**/*.vm",
              "**/*.log",
              "**/*.map",
              "**/*.js.map",
              "**/*.tmpl",
              "**/*.js.tmpl",
              "**/*.editorconfig",
              "**/*.lombok.config",
              "src/test/resources/**",
              "src/main/resources/**",
              "out/**",
              "build/**"])
}

// workhack for license issue #76
gradle.startParameter.excludedTaskNames += "licenseMain"
gradle.startParameter.excludedTaskNames += "licenseTest"

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from(sourceSets.main.allJava)
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set("javadoc")
    javadoc.properties.failOnError = true
}

def releaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "${rootProject.name}"
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "${rootProject.name}"
                description = "Project that holds utility class for file operations"
                url = "https://github.com/lightblueseas/"+"${rootProject.name}"
                organization {
                    name = "Alpha Ro Group UG (haftungsbeschr√§ngt)"
                    url = "http://www.alpharogroup.de/"
                }
                issueManagement {
                    system = "GitHub"
                    url = "https://github.com/lightblueseas/"+"${rootProject.name}"+"/issues"
                }
                licenses {
                    license {
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "astrapi69"
                        name = "Asterios Raptis"
                    }
                }
                scm {
                    connection = "scm:git:git:@github.com:lightblueseas/"+"${rootProject.name}"+".git"
                    developerConnection = "scm:git:git@github.com:lightblueseas/"+"${rootProject.name}"+".git"
                    url = "git:@github.com:lightblueseas/"+"${rootProject.name}"+".git"
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username System.getenv("ossrhUsername") ?: project.property("ossrhUsername")
                password System.getenv("ossrhPassword") ?: project.property("ossrhPassword")
            }
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = releaseVersion ? releasesRepoUrl : snapshotsRepoUrl
        }
    }
}

signing {
    if (releaseVersion) {
        sign publishing.publications.mavenJava
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

test {
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ["de/alpharogroup/file/exceptions/**/*.class",
                              "de/alpharogroup/file/compare/api/**/*.class",
                              "de/alpharogroup/file/modify/api/**/*.class",
                              "de/alpharogroup/file/FileConst.class",
                              "de/alpharogroup/file/zip/ZipState.class",
                              "de/alpharogroup/file/zip/ZipModel.class"])
        }))
    }
}

check.dependsOn jacocoTestReport
