
plugins {
    id "io.franzbecker.gradle-lombok" version "3.2.0"
    id "java"
    id "maven-publish"
    id "signing"
    id "com.github.ben-manes.versions" version "0.27.0"
}

group = "de.alpharogroup"
version = "5.4-SNAPSHOT"
description = "file-worker"
sourceCompatibility = "1.8"

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

ext {
    lombokVersion = "1.18.10"
    mysticCryptVersion = "7.1.1"
    commonsIoVersion = "2.6"
    commonsLang3Version = "3.9"
    zip4jVersion = "1.3.2"
    sillyStringsVersion = "5.3.1"
    sillyIoVersion = "1.1"
    sillyCollectionsVersion = "5.4"
    cryptApiVersion = "7.2"
    jobjContractVerifierVersion = "3.2"
    testObjectsVersion = "5.2"
    meanbeanVersion = "2.0.3"
    meanbeanFactoriesVersion = "1.1.1"
    testngVersion = "7.0.0"
    junitVersion = "4.12"
    mockitoCoreVersion = "3.1.0"
}

dependencies {
    compile "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    compile "commons-io:commons-io:${commonsIoVersion}"
    compile "org.apache.commons:commons-lang3:${commonsLang3Version}"
    compile "net.lingala.zip4j:zip4j:${zip4jVersion}"
    compile "de.alpharogroup:silly-strings:${sillyStringsVersion}"
    compile "de.alpharogroup:silly-io:${sillyIoVersion}"
    compile "de.alpharogroup:silly-collections:${sillyCollectionsVersion}"
    compile "de.alpharogroup:crypt-api:${cryptApiVersion}"
    compile "de.alpharogroup:mystic-crypt:${mysticCryptVersion}"
    testCompile "de.alpharogroup:jobj-contract-verifier:${jobjContractVerifierVersion}"
    testCompile "de.alpharogroup:test-objects:${testObjectsVersion}"
    testCompile "org.meanbean:meanbean:${meanbeanVersion}"
    testCompile "de.alpharogroup:meanbean-factories:${meanbeanFactoriesVersion}"
    testCompile "org.testng:testng:${testngVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.mockito:mockito-core:${mockitoCoreVersion}"
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from(sourceSets.main.allJava)
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set("javadoc")
    javadoc.properties.failOnError = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "${rootProject.name}"
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "${rootProject.name}"
                description = "Project that holds utility class for file operations"
                url = "https://github.com/lightblueseas/"+"${rootProject.name}"
                organization {
                    name = "Alpha Ro Group UG (haftungsbeschr√§ngt)"
                    url = "http://www.alpharogroup.de/"
                }
                issueManagement {
                    system = "GitHub"
                    url = "https://github.com/lightblueseas/"+"${rootProject.name}"+"/issues"
                }
                licenses {
                    license {
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "astrapi69"
                        name = "Asterios Raptis"
                    }
                }
                scm {
                    connection = "scm:git:git:@github.com:lightblueseas/"+"${rootProject.name}"+".git"
                    developerConnection = "scm:git:git@github.com:lightblueseas/"+"${rootProject.name}"+".git"
                    url = "git:@github.com:lightblueseas/"+"${rootProject.name}"+".git"
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username project.property("ossrhUsername")
                password project.property("ossrhPassword")
            }
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

test {
    useTestNG()
}
